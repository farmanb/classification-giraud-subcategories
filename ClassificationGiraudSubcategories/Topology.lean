/-
Copyright (c) 2025 Blake Farman. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Blake Farman
-/
import ClassificationGiraudSubcategories.Basic
import Mathlib.Topology.Algebra.LinearTopology
import Mathlib.Topology.Algebra.FilterBasis
/-!
# Topologies associated to ideal filters

This file constructs topological structures on a ring from an `IdealFilter`.

## Main definitions

* `IdealFilter.addGroupFilterBasis`: the additive-group filter basis whose sets are the ideals in
  `F`.
* `IdealFilter.addGroupTopology`: the corresponding topology on `A`.
* `IdealFilter.ringFilterBasis`: under `F.IsUniform`, a ring filter basis.
* `IdealFilter.ringTopology`: under `F.IsUniform`, the corresponding ring topology on `A`.

## Main statements

* `IdealFilter.mem_nhds_iff`: a neighbourhood characterization for `addGroupTopology`.
* `IdealFilter.isLinearTopology_ringTopology`: the topology `ringTopology` is linear.

## Implementation notes

This file focuses on constructing the topology associated to an ideal filter. For background on the
relationship between linear topologies, uniform filters, and the Gabriel condition (T4), see the
`## Implementation notes` section of `ClassificationGiraudSubcategories.Basic`.

## References

* [nLab: Uniform filter](https://ncatlab.org/nlab/show/uniform+filter)
* [nLab: Gabriel filter](https://ncatlab.org/nlab/show/Gabriel+filter)
* [nLab: Gabriel composition](https://ncatlab.org/nlab/show/Gabriel+composition+of+filters)

## Tags

ring theory, ideal, filter, linear topology
-/

open scoped Pointwise
open scoped Topology

namespace IdealFilter

variable {A : Type u} [Ring A] (F : IdealFilter A)

/-- The additive-group filter basis whose sets are the ideals belonging to the ideal filter `F`. -/
def addGroupFilterBasis : AddGroupFilterBasis A where
  sets := {(I : Set A) | I âˆˆ F}
  nonempty := by
    obtain âŸ¨I, h_IâŸ© := F.nonempty
    exact âŸ¨I, âŸ¨I, h_I, rflâŸ©âŸ©
  inter_sets := by
    rintro s t âŸ¨I, h_I, rflâŸ© âŸ¨J, h_J, rflâŸ©
    refine âŸ¨I âŠ“ J, âŸ¨I âŠ“ J, Order.PFilter.inf_mem h_I h_J, rflâŸ©, fun x h â†¦ hâŸ©
  zero' := by
    rintro s âŸ¨I, h_I, rflâŸ©
    exact zero_mem I
  add' := by
    rintro s âŸ¨I, h_I, rflâŸ©
    refine âŸ¨I, âŸ¨I, h_I, rflâŸ©, Set.add_subset_iff.mpr ?_âŸ©
    exact fun x a y a_1 â†¦ add_mem a a_1
  neg' := by
    rintro s âŸ¨I, h_I, rflâŸ©
    exact âŸ¨I, âŸ¨I, h_I, rflâŸ©, by simpâŸ©
  conj' := by
    rintro xâ‚€ s âŸ¨I, h_I, rflâŸ©
    exact âŸ¨I, âŸ¨I, h_I, rflâŸ©, by simpâŸ©

/-- Under `F.IsUniform`, the ring filter basis obtained from `addGroupFilterBasis`.
The right-multiplication axiom uses the colon-closure from uniformity. -/
def ringFilterBasis (uni_F : F.IsUniform) : RingFilterBasis A where
  sets := F.addGroupFilterBasis.sets
  nonempty := F.addGroupFilterBasis.nonempty
  inter_sets := F.addGroupFilterBasis.inter_sets
  zero' := F.addGroupFilterBasis.zero'
  add' := F.addGroupFilterBasis.add'
  neg' := F.addGroupFilterBasis.neg'
  conj' := F.addGroupFilterBasis.conj'
  mul' := by
    rintro s âŸ¨I, h_I, rflâŸ©
    exact âŸ¨I, âŸ¨I, h_I, rflâŸ©, Set.mul_subset_iff.mpr fun _ hâ‚ _ hâ‚‚ => mul_mem hâ‚ hâ‚‚âŸ©
  mul_left' := by
    rintro xâ‚€ s âŸ¨I, h_I, rflâŸ©
    refine âŸ¨I, âŸ¨I, h_I, rflâŸ©, ?_âŸ©
    intro x h_x
    exact Ideal.mul_mem_left I xâ‚€ h_x
  mul_right' := by
    rintro xâ‚€ s âŸ¨I, h_I, rflâŸ©
    refine âŸ¨I.colon (Ideal.span {xâ‚€}), ?_, ?_âŸ©
    Â· exact âŸ¨I.colon (Ideal.span {xâ‚€}), uni_F.colon_mem h_I xâ‚€, rflâŸ©
    Â· intro x h_x
      exact Submodule.mem_colon.mp h_x xâ‚€ (Ideal.mem_span_singleton_self xâ‚€)

/-- The topology on `A` induced by `addGroupFilterBasis`. -/
def addGroupTopology : TopologicalSpace A := (addGroupFilterBasis F).topology

/-- The topology `addGroupTopology` makes `A` a topological additive group. -/
theorem isTopologicalAddGroup :
    letI : TopologicalSpace A := F.addGroupTopology
    IsTopologicalAddGroup A :=
  F.addGroupFilterBasis.isTopologicalAddGroup

/-- Under `F.IsUniform`, the topology on `A` induced by `ringFilterBasis`. -/
def ringTopology (uni_F : F.IsUniform) : TopologicalSpace A :=
  (ringFilterBasis F uni_F).topology

/-- Under `F.IsUniform`, the topology `ringTopology` makes `A` a topological ring. -/
theorem isTopologicalRing (uni_F : F.IsUniform) :
    letI : TopologicalSpace A := F.ringTopology uni_F
    IsTopologicalRing A :=
  (F.ringFilterBasis uni_F).isTopologicalRing

/-- Neighbourhoods in `addGroupTopology`: a set is a neighbourhood of `a` iff it contains a
left-additive coset `a +áµ¥ I` for some ideal `I âˆˆ F`. -/
lemma mem_nhds_iff (a : A) (s : Set A) :
    letI : TopologicalSpace A := F.addGroupTopology
    s âˆˆ ð“ a â†” âˆƒ I âˆˆ F, a +áµ¥ (I : Set A) âŠ† s := by
  constructor
  Â· intro h_s
    rcases ((F.addGroupFilterBasis).nhds_hasBasis a).mem_iff.1 h_s with âŸ¨t, ht, htsâŸ©
    rcases ht with âŸ¨I, hI, rflâŸ©
    exact âŸ¨I, hI, htsâŸ©
  Â· rintro âŸ¨I, hI, hIsâŸ©
    refine ((F.addGroupFilterBasis).nhds_hasBasis a).mem_iff.2 ?_
    exact âŸ¨(I : Set A), âŸ¨I, hI, rflâŸ©, hIsâŸ©

/-- Under `F.IsUniform`, the topology `ringTopology` is linear in the sense that `ð“ 0` has a
basis of ideals. -/
theorem isLinearTopology_ringTopology (uni_F : F.IsUniform) :
    letI : TopologicalSpace A := F.ringTopology uni_F
    IsLinearTopology A A := by
  letI : TopologicalSpace A := F.ringTopology uni_F
  have hBasis :
      (ð“ (0 : A)).HasBasis (fun I : Ideal A => I âˆˆ F) (fun I : Ideal A => (I : Set A)) := by
    refine âŸ¨?_âŸ©
    intro U
    simpa using (F.mem_nhds_iff (a := (0 : A)) (s := U))
  refine IsLinearTopology.mk_of_hasBasis' (R := A) (M := A)
      (Î¹ := Ideal A) (S := Ideal A)
      (p := fun I : Ideal A => I âˆˆ F) (s := fun I : Ideal A => I)
      ?_ ?_
  Â· simpa using hBasis
  Â· intro I r m hm
    simpa using Ideal.mul_mem_left I r hm
end IdealFilter
