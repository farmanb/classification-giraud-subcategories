/-
Copyright (c) 2025 Blake Farman. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Blake Farman
-/
import Mathlib.GroupTheory.Coset.Basic
import Mathlib.Order.PFilter
import Mathlib.RingTheory.Ideal.Basic
import Mathlib.RingTheory.Ideal.Colon
import Mathlib.Topology.Algebra.Group.Defs
import Mathlib.Topology.Algebra.LinearTopology
import Mathlib.Topology.Algebra.Monoid.Defs
import Mathlib.Topology.Algebra.Ring.Basic
import Mathlib.Topology.Algebra.FilterBasis
import Mathlib.Tactic.Abel
import Mathlib.Tactic.NoncommRing
--import Mathlib.Topology.Defs.Filter

open scoped Topology
open Filter
open scoped Pointwise

--import Mathlib.Topology.Defs.Basic
/-!
# Ideal Filters

An **ideal filter** is a filter in the lattice of ideals of a ring `A`.

## Main definitions

* `IdealFilter A`: the type of an ideal filter on a ring `A`.
* `IsUniform F` : a filter `F` is uniform if whenever `I` is an ideal in the filter, then for all
`a : A`, the colon ideal `(I : a)` is in `F`.
* `IsTorsionElem` : Given a filter `F`, an element, `m`, of an `A`-module, `M`, is `F`-torsion if
there exists an ideal `L` in `F` that annihilates `m`.
* `IsTorsion` : Given a filter `F`, an `A`-module, `M`, is torsion if every element is torsion.
* `GabrielComposition` : Given two filters `F` and `G`, the Gabriel composition of `F` and `G` is
the set of ideals `L` of `A` such that there exists an ideal `K` in `G` with `K/L` `F`-torsion.
This is again a filter.
* `IsGabriel F` : a filter `F` is uniform if

## References

* [Bo Stenstr√∂m, Rings and Modules of Quotients][stenstrom1971]
* https://ncatlab.org/nlab/show/uniform+filter
* https://ncatlab.org/nlab/show/Gabriel+filter
* https://ncatlab.org/nlab/show/Gabriel+composition+of+filters

## Tags

category theory, ideal, filter, ultrafilter, Gabriel filter
-/
universe u v

abbrev IdealFilter (A : Type u) [Ring A] := Order.PFilter (Ideal A)

--abbrev sets {A : Type u} [Ring A] (F : IdealFilter A) : Set (Ideal A) := F.carrier
/- variable (A : Type u) [Ring A] (F : IdealFilter A)
#check  -/


namespace IdealFilter

variable {A : Type u} [Ring A]

--structure IsUniform {A : Type u} [Ring A] (F : IdealFilter A) : Prop where
structure IsUniform (F : IdealFilter A) : Prop where
   (colon_closed : ‚àÄ {I : Ideal A}, I ‚àà F ‚Üí
      ‚àÄ a : A, (I.colon (Ideal.span {a})) ‚àà F)

/-- We say that an element `m : M` is `F`-torsion if it is annihilated by some ideal belonging to
the filter `F`.  That is, there exists `L ‚àà F` such that every `a ‚àà L` satisfies
`a ‚Ä¢ m = 0`. -/
def IsTorsionElem (F : IdealFilter A)
      {M : Type v} [AddCommMonoid M] [Module A M] (m : M) : Prop :=
   ‚àÉ L ‚àà F, ‚àÄ a ‚àà L, a ‚Ä¢ m = 0

/-- We say that an `A`-module `M` is `F`-torsion if every element of `M` is `F`-torsion in the
sense of `IsTorsionElem`. -/
def IsTorsion (F : IdealFilter A)
      (M : Type v) [AddCommMonoid M] [Module A M] : Prop :=
   ‚àÄ m : M, IsTorsionElem F m

/-- We say that the quotient `K/L` is `F`-torsion if every element `k ‚àà K` is annihilated
(modulo `L`) by some ideal in `F`.  Equivalently, for each `k ‚àà K` there exists `I ‚àà F`
such that `I ‚â§ L.colon (Ideal.span {k})`. That is to say, every `a ‚àà I` satisfies `a * k ‚àà L`.
This formulation avoids forming the quotient module explicitly. -/
def IsTorsionQuot (F : IdealFilter A) (L K : Ideal A) : Prop :=
   ‚àÄ k ‚àà K, ‚àÉ I ‚àà F, I ‚â§ L.colon (Ideal.span {k})

/-- If `k ‚àà K`, then intersecting with `K` does not change the colon ideal. That is to say, there is
an equality of colon ideals: `(L : k) = (L ‚äì K : k)`. -/
lemma colon_inf_eq_for_mem (L K : Ideal A) {k : A} (h_k : k ‚àà K) :
    (L ‚äì K).colon (Ideal.span ({k} : Set A)) = L.colon (Ideal.span ({k} : Set A)) := by
  -- ext `a : A` and unpack `Submodule.mem_colon`
  ext a
  constructor <;> intro h_a
  ¬∑ -- `a ‚àà (L ‚äì K).colon(span{k})` ‚áí `a ‚àà L.colon(span{k})`
    -- use Submodule.mem_colon to rewrite membership
    rcases (Submodule.mem_colon).1 h_a with h
    -- need: ‚àÄ p ‚àà span{k}, a ‚Ä¢ p ‚àà L
    apply (Submodule.mem_colon).2
    intro p h_p
    -- p ‚àà span{k} ‚áí p = r * k
    obtain ‚ü®r, rfl‚ü© := Ideal.mem_span_singleton'.1 h_p
    -- from h we know: a ‚Ä¢ (r * k) ‚àà L ‚äì K
    specialize h (r * k) ?_
    ¬∑ exact h_p
    ¬∑ rcases h with ‚ü®h_L, h_K‚ü©
      exact h_L
  ¬∑ -- same in the other direction, using that k ‚àà K gives automatic K-membership
    rcases (Submodule.mem_colon).1 h_a with h
    apply (Submodule.mem_colon).2
    intro p h_p
    obtain ‚ü®r, rfl‚ü© := Ideal.mem_span_singleton'.1 h_p
    -- a ‚Ä¢ (r * k) ‚àà L by h
    have h_L : a ‚Ä¢ (r * k) ‚àà L := h (r*k) h_p
    -- and also a ‚Ä¢ (r * k) ‚àà K because k ‚àà K and K is an ideal
    have h_K : a ‚Ä¢ (r * k) ‚àà K := by
      -- use closure of K under multiplication by scalars and membership of k
      -- this is just Ideal.mul_mem_left followed by ring simp
      simpa [mul_assoc, smul_mul_assoc] using
        Ideal.mul_mem_left K a (Ideal.mul_mem_left K r h_k)
    exact ‚ü®h_L, h_K‚ü©

/-- Intersecting the left ideal with `K` does not change `IsTorsionQuot` on the right. -/
@[simp]
lemma IsTorsionQuot_inter_left_iff
     (F : IdealFilter A)
    (L K : Ideal A) :
    IsTorsionQuot F L K ‚Üî IsTorsionQuot F (L ‚äì K) K := by
  unfold IsTorsionQuot
  constructor
  ¬∑ intro h k h_k
    -- use the witness from `h`, then rewrite the colon using the lemma
    rcases h k h_k with ‚ü®I, h_I, h_I_le‚ü©
    refine ‚ü®I, h_I, ?_‚ü©
    -- `I ‚â§ L.colon(span{k})` and those two colon ideals are equal
    ¬∑ have hcol :=
        colon_inf_eq_for_mem (L := L) (K := K) (k := k) h_k
      simpa [hcol] using h_I_le
  ¬∑ intro h k h_k
    rcases h k h_k with ‚ü®I, h_I, h_I_le‚ü©
    refine ‚ü®I, h_I, ?_‚ü© -- now use equality in the opposite direction
    ¬∑ have hcol := colon_inf_eq_for_mem (L := L) (K := K) (k := k) h_k
      simpa [hcol] using h_I_le

@[simp] lemma IsTorsion_def (F : IdealFilter A)
      (M : Type v) [AddCommMonoid M] [Module A M] : IsTorsion F M ‚Üî ‚àÄ m : M, IsTorsionElem F m :=
  Iff.rfl

@[simp] lemma IsTorsionQuot_def (F : IdealFilter A) (L K : Ideal A) :
      IsTorsionQuot F L K ‚Üî ‚àÄ k ‚àà (K : Set A), ‚àÉ I ‚àà F, I ‚â§ L.colon (Ideal.span {k}) :=
  Iff.rfl

/-- If `x ‚àà I`, then the colon ideal `(x : I)` is the whole ring. -/
lemma colon_span_singleton_eq_top_of_mem {I : Ideal A} {x : A} (h_x : x ‚àà I) :
    I.colon (Ideal.span {x}) = ‚ä§ := by
  apply (Ideal.eq_top_iff_one (I.colon (Ideal.span {x}))).mpr
  apply Submodule.mem_colon.mpr
  intro p h_p
  obtain ‚ü®a,rfl‚ü© := Ideal.mem_span_singleton'.mp h_p
  simp only [one_smul,Ideal.mul_mem_left,h_x]

/-- For any filter `F` and ideal `J`, the quotient `J/J` is `F`-torsion in the sense of
`IsTorsionQuot`. -/
lemma IsTorsionQuot_self (F : IdealFilter A) (I : Ideal A) :
    IsTorsionQuot F I I := by
  intro x h_x
  obtain ‚ü®J, h_J‚ü© := F.nonempty
  exact ‚ü®J, h_J, by simp[colon_span_singleton_eq_top_of_mem h_x]‚ü©

lemma IsTorsionQuot_mono_left (F : IdealFilter A)
    {I J K : Ideal A} (I_leq_J : I ‚â§ J) : IsTorsionQuot F I K ‚Üí IsTorsionQuot F J K := by
  intro I_tors x h_x
  obtain ‚ü®L, ‚ü®L_in_F, h_L‚ü©‚ü© := I_tors x h_x
  exact ‚ü®L, L_in_F, fun y h_y ‚¶Éa‚¶Ñ a_1 ‚Ü¶ I_leq_J (h_L h_y a_1)‚ü©

lemma isPFilter (F G : IdealFilter A) :
    Order.IsPFilter {L : Ideal A | ‚àÉ K ‚àà G, F.IsTorsionQuot L K} := by
    refine Order.IsPFilter.of_def ?nonempty ?directed ?mem_of_le
    ¬∑ obtain ‚ü®J,h_J‚ü© := G.nonempty
      exact ‚ü®J, J, h_J, IsTorsionQuot_self F J‚ü©
    ¬∑ rintro I ‚ü®K, h_K, h_IK‚ü© J ‚ü®L, h_L, h_JL‚ü©
      refine ‚ü®I ‚äì J, ?_, inf_le_left, inf_le_right‚ü©
      ¬∑ refine ‚ü®K ‚äì L, ?_, ?_‚ü©
        ¬∑ exact Order.PFilter.inf_mem h_K h_L
        ¬∑ rintro x h_x
          rcases h_x with ‚ü®x_K, x_L‚ü©
          obtain ‚ü®K‚ÇÅ, h_K‚ÇÅF, h_K‚ÇÅ‚ü© := h_IK x x_K
          obtain ‚ü®K‚ÇÇ, h_K‚ÇÇF, h_K‚ÇÇ‚ü© := h_JL x x_L
          refine ‚ü®K‚ÇÅ ‚äì K‚ÇÇ, Order.PFilter.inf_mem h_K‚ÇÅF h_K‚ÇÇF, ?_‚ü©
          rintro y ‚ü®h_y‚ÇÅ, h_y‚ÇÇ‚ü©
          have h‚ÇÅ := Submodule.mem_colon.mp (h_K‚ÇÅ h_y‚ÇÅ)
          have h‚ÇÇ := Submodule.mem_colon.mp (h_K‚ÇÇ h_y‚ÇÇ)
          exact Submodule.mem_colon.mpr (fun p h_p => ‚ü®h‚ÇÅ p h_p, h‚ÇÇ p h_p‚ü©)
    ¬∑ intro I J h_IJ ‚ü®K, h_K, h_IK‚ü©
      exact ‚ü®K, h_K, IsTorsionQuot_mono_left F h_IJ h_IK‚ü©

def GabrielComposition (F G : IdealFilter A) : IdealFilter A := (isPFilter F G).toPFilter

-- Declare notation for Gabriel composition
infixl:70 " ‚Ä¢ " => GabrielComposition

structure IsGabriel (F : IdealFilter A) extends IsUniform F where
    gabriel_closed : ‚àÄ (I : Ideal A), (‚àÉ J ‚àà F, ‚àÄ x ‚àà J, I.colon (Ideal.span {x}) ‚àà F) ‚Üí
    I ‚àà F

theorem isGabriel_iff (F : IdealFilter A) :
    F.IsGabriel ‚Üî F.IsUniform ‚àß F ‚Ä¢ F = F := by
  constructor
  ¬∑ rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
    refine ‚ü®h‚ÇÅ, ?_‚ü©
    ext I
    constructor <;> intro h_I
    ¬∑ rcases h_I with ‚ü®J,h_J, h_tors‚ü©
      unfold IsTorsionQuot at h_tors
      refine h‚ÇÇ I ‚ü®J, h_J, ?_‚ü©
      intro x h_x
      rcases h_tors x h_x with ‚ü®K, h_K, h_incl‚ü©
      exact Order.PFilter.mem_of_le h_incl h_K
    ¬∑ exact ‚ü®I, h_I, IsTorsionQuot_self F I‚ü©
  ¬∑ rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
    refine ‚ü®h‚ÇÅ, ?_‚ü©
    rintro I ‚ü®J, h_J, h_colon‚ü©
    rw[‚Üêh‚ÇÇ]
    refine ‚ü®J, h_J,?_‚ü©
    intro x h_x
    exact ‚ü®I.colon (Ideal.span {x}), h_colon x h_x, by rfl‚ü©

section Topology
variable (F : IdealFilter A)

def addGroupFilterBasis : AddGroupFilterBasis A where
  sets := {(I : Set A) | I ‚àà F}
  nonempty := by
    obtain ‚ü®I, h_I‚ü© := F.nonempty
    exact ‚ü®I, ‚ü®I,h_I, rfl‚ü©‚ü©
  inter_sets := by
    rintro s t ‚ü®I, h_I, rfl‚ü© ‚ü®J, h_J, rfl‚ü©
    refine ‚ü®I ‚äì J, ‚ü®I ‚äì J, Order.PFilter.inf_mem h_I h_J, rfl‚ü©, ?_‚ü©
    intro x h
    exact h
  zero' := by
    rintro s ‚ü®I,h_I,rfl‚ü©
    exact zero_mem I
  add' := by
    rintro s ‚ü®I, h_I, rfl‚ü©
    refine ‚ü®I, ‚ü®I, h_I,rfl‚ü©, Set.add_subset_iff.mpr ?_‚ü©
    exact fun x a y a_1 ‚Ü¶ add_mem a a_1
  neg' := by
    rintro s ‚ü®I, h_I, rfl‚ü©
    exact ‚ü®I, ‚ü®I, h_I, rfl‚ü©, by simp‚ü©
  conj' := by
    rintro x‚ÇÄ s ‚ü®I,h_I,rfl‚ü©
    exact ‚ü®I, ‚ü®I, h_I, rfl‚ü©,by simp‚ü©

def ringFilterBasis (uni_F : IsUniform F) : RingFilterBasis A where
  sets := F.addGroupFilterBasis.sets
  nonempty := F.addGroupFilterBasis.nonempty
  inter_sets := F.addGroupFilterBasis.inter_sets
  zero' := F.addGroupFilterBasis.zero'
  add' := F.addGroupFilterBasis.add'
  neg' := F.addGroupFilterBasis.neg'
  conj' := F.addGroupFilterBasis.conj'
  mul' := by
    rintro s ‚ü®I, h_I, rfl‚ü©
    exact ‚ü®I, ‚ü®I, h_I, rfl‚ü©, Set.mul_subset_iff.mpr fun _ h‚ÇÅ _ h‚ÇÇ => mul_mem h‚ÇÅ h‚ÇÇ‚ü©
  mul_left' := by
    rintro x‚ÇÄ s ‚ü®I, h_I, rfl‚ü©
    refine ‚ü®I, ‚ü®I,h_I,rfl‚ü©, ?_‚ü©
    intro x h_x
    exact Ideal.mul_mem_left I x‚ÇÄ h_x
  mul_right' := by
    rintro x‚ÇÄ s ‚ü®I, h_I, rfl‚ü©
    refine ‚ü®I.colon (Ideal.span {x‚ÇÄ}), ?_, ?_‚ü©
    ¬∑ exact ‚ü®I.colon (Ideal.span {x‚ÇÄ}), uni_F.colon_closed h_I x‚ÇÄ, rfl‚ü©
    ¬∑ intro x h_x
      exact Submodule.mem_colon.mp h_x x‚ÇÄ (Ideal.mem_span_singleton_self x‚ÇÄ)

def topology : TopologicalSpace A := (addGroupFilterBasis F).topology

def isTopologicalAddGroup :
    letI : TopologicalSpace A := F.topology
    IsTopologicalAddGroup A :=
  F.addGroupFilterBasis.isTopologicalAddGroup

def ringTopology (uni_F : IsUniform F) : TopologicalSpace A :=
  (ringFilterBasis F uni_F).topology

def isTopologicalRing (uni_F : F.IsUniform) :
    letI : TopologicalSpace A := F.ringTopology uni_F
    IsTopologicalRing A :=
  (F.ringFilterBasis uni_F).isTopologicalRing

/-- In the topology on `A` induced by an ideal filter `F`, every translate `x +·µ• I` of an ideal
`I ‚àà F` is an open neighborhood of `x`.

This is the basic ‚Äúlinear‚Äù feature of `topology_of_IdealFilter`: neighborhoods are generated by
(translates of) ideals coming from the filter. -/
lemma isOpen_leftAddCoset (x : A) {I : Ideal A} (h_I : I ‚àà F) :
    letI : TopologicalSpace A := F.topology
    IsOpen (x +·µ• (I : Set A)) := by
  letI : TopologicalSpace A := F.topology
  refine (isOpen_iff_mem_nhds).mpr ?_
  rintro y ‚ü®z, h_z, rfl‚ü©
  have : x +·µ• (I : Set A) = (x + z) +·µ• (I : Set A) := by
    have : z +·µ• (I : Set A) = (I : Set A) :=
      leftAddCoset_mem_leftAddCoset I.toAddSubgroup h_z
    rw[‚Üê leftAddCoset_assoc,this]
  rw[this]
  refine ((F.addGroupFilterBasis).nhds_hasBasis (x + z)).mem_iff.2 ?_
  refine ‚ü®(I : Set A), ?_, ?_‚ü©
  ¬∑ exact ‚ü®I, h_I, rfl‚ü©
  ¬∑ intro y hy; exact hy

lemma mem_nhds_iff (a : A) (s : Set A) :
    letI : TopologicalSpace A := F.topology
    s ‚àà ùìù a ‚Üî ‚àÉ I ‚àà F, a +·µ• (I : Set A) ‚äÜ s := by
  constructor
  ¬∑ intro h_s
    rcases ((F.addGroupFilterBasis).nhds_hasBasis a).mem_iff.1 h_s with ‚ü®t, ht, hts‚ü©
    rcases ht with ‚ü®I, hI, rfl‚ü©
    exact ‚ü®I, hI, hts‚ü©
  ¬∑ rintro ‚ü®I, hI, hIs‚ü©
    refine ((F.addGroupFilterBasis).nhds_hasBasis a).mem_iff.2 ?_
    exact ‚ü®(I : Set A), ‚ü®I, hI, rfl‚ü©, hIs‚ü©

/-- A set is open in `topology_of_IdealFilter F` iff it contains a basic coset neighborhood around
each of its points. -/
lemma isOpen_iff_exists_leftAddCoset_subset (s : Set A) :
    letI : TopologicalSpace A := F.topology
    IsOpen s ‚Üî ‚àÄ a ‚àà s, ‚àÉ I ‚àà F, a +·µ• (I : Set A) ‚äÜ s := by
  letI : TopologicalSpace A := F.topology
  constructor
  ¬∑ intro h_s a h_a
    rw[‚Üê F.mem_nhds_iff a s]
    exact IsOpen.mem_nhds h_s h_a
  ¬∑ intro h
    refine (isOpen_iff_mem_nhds).2 ?_
    intro a h_a
    exact (F.mem_nhds_iff a s).2 (h a h_a)


end Topology

end IdealFilter
